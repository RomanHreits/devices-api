plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'devices-api'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    functionalTest {
        java.srcDir file('src/functionalTest/java')
        resources.srcDirs = [file('src/functionalTest/resources'), file('src/test/resources')]

        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testCompileClasspath
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('functionalTest', Test) {
    description = 'Runs functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'test'

    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.testcontainers:postgresql:1.21.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    functionalTestImplementation 'org.springframework.boot:spring-boot-starter-restclient'
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    enabled = true
}

tasks.named('check') {
    dependsOn tasks.named('functionalTest')
}